plugins {
  id 'io.codearte.nexus-staging' version '0.11.0'
  id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'signing'

afterReleaseBuild.dependsOn uploadArchives

group = 'com.nordstrom.ui-tools'
version = '14.1.7-SNAPSHOT'

description = """Selenium Foundation"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

jacoco {
  toolVersion = "0.7.9"
  reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
  reports {
    xml.enabled false
    csv.enabled false
    html.destination file("${buildDir}/jacocoHtml")
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Selenium Foundation'
        packaging 'jar'
        description 'Selenium Foundation is an automation framework designed to extend and enhance the capabilities provided by Selenium 2 (WebDriver).'
        url 'https://github.com/Nordstrom/Selenium-Foundation'

        scm {
          connection 'scm:git:https://github.com/Nordstrom/Selenium-Foundation.git'
          developerConnection 'scm:git:https://github.com/Nordstrom/Selenium-Foundation.git'
          url 'https://github.com/Nordstrom/Selenium-Foundation/tree/master'
          tag 'HEAD'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'scoba'
            name 'Scott Babcock'
            email 'scoba@hotmail.com'
            organization 'Nordstrom'
            organizationUrl 'https://shop.nordstrom.com'
          }
        }
      }
    }
  }
}

nexusStaging {
  packageGroup = "com.nordstrom"
  stagingProfileId = "76d943f622957"
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

repositories {
   maven { url "http://central" }
   maven { url "https://mvnrepo.nordstrom.net/nexus/content/repositories/thirdparty" }
   maven { url "http://public-snapshots" }
   maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
  compile group: 'com.nordstrom.tools', name: 'java-utils', version:'1.3.3'
  compile group: 'com.nordstrom.tools', name: 'settings', version:'2.0.7'
  compile group: 'com.nordstrom.tools', name: 'testng-foundation', version:'1.5.1'
  compile group: 'com.nordstrom.tools', name: 'junit-foundation', version:'3.1.5'
  compile group: 'org.seleniumhq.selenium', name: 'selenium-server-standalone', version:'2.53.1'
  compile group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version:'3.1.2.Final'
  compile group: 'org.jboss.resteasy', name: 'resteasy-client', version:'3.1.2.Final'
  compile group: 'net.bytebuddy', name: 'byte-buddy', version:'1.7.9'
  compile group: 'ch.qos.logback', name: 'logback-classic', version:'1.2.2'
  compile group: 'com.github.sbabcoc', name: 'logback-testng', version:'1.0.0'
  testCompile group: 'org.mockito', name: 'mockito-all', version:'1.10.19'
}
